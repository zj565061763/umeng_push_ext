plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libGroupId = "com.sd.lib.umeng"
def libArtifactId = "umeng_push_ext"
def libVersionCode = 20210702
def libVersionName = "6.3.3-v01"

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode libVersionCode
        versionName libVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    //华为厂商通道
    api 'com.umeng.umsdk:huawei-umengaccs:1.3.4'
    api 'com.huawei.hms:push:5.3.0.301'

    //小米厂商通道
    api 'com.umeng.umsdk:xiaomi-umengaccs:1.2.4'
    api 'com.umeng.umsdk:xiaomi-push:4.0.2'

    //魅族厂商通道
    api 'com.umeng.umsdk:meizu-umengaccs:1.1.4'
    api 'com.umeng.umsdk:meizu-push:4.0.7'

    //OPPO厂商通道
    api 'com.umeng.umsdk:oppo-umengaccs:1.0.7-fix'
    api 'com.umeng.umsdk:oppo-push:2.1.0'

    //vivo厂商通道
    api 'com.umeng.umsdk:vivo-umengaccs:1.1.4'
    api 'com.umeng.umsdk:vivo-push:3.0.0'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}